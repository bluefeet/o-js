#!/usr/bin/env perl
use strict;
use warnings FATAL => 'all';

use autodie;
use FindBin qw( $Bin );
use Getopt::Long;
use Pod::Usage qw( pod2usage );

GetOptions(
    'help'       => \my $help,
    'repo-dir=s' => \my $repo_dir,
    'version=s'  => \my $version,
) or die "Unable to process options.\n";

if ($help) {
    pod2usage( -verbose => 2 );
    exit 0;
}

$repo_dir ||= "$Bin/..";

if (!$version) {
    my $string = `cd $repo_dir && git tag`;
    my @versions = (
        map { $_->[1] }
        sort { $b->[0] <=> $a->[0] }
        map { [ ($_->[0]*1000000)+($_->[1]*1000)+$_->[2], $_->[3] ] }
        map { my $v=$_; ($v =~ m{^v(\d+)\.(\d+)\.(\d+)$}s) ? [$1,$2,$3,$v] : () }
        split(/\s+/s, $string)
    );
    $version = shift( @versions );
    die "Unable to find the most recent version using 'git tag'!\n" if !$version;
}

die "Invalid version $version!\n" if $version !~ m{^v\d+\.\d+\.\d+$}s;

print "// o.js : $version : http://o-js.com : MIT License\n";

__END__

=head1 NAME

version-header - Prints out the version header to be included in combined files.

=head1 SYNOPSIS

    bin/version-header

=head1 DESCRIPTION

The combined C<o.js> and C<o-min.js> files have a version header at
the top that includes the version of o.js, the URL to the website,
and the license declaration.  This script print this version header
and is used by both the combine and minify scripts.

=head1 ARGUMENTS

=head2 help

    bin/version-header --help

Shows this documentation.

=head2 repo-dir

    bin/version-header --repo-dir=<path-to-o-js-repo>

The directory where the o-js repo is which will be used for
finding the most recent version tag if none is specified.
Defaults to C<$script_dir/..>.

=head2 version

    bin/version-header --version=<o-js-version>

The version of o.js to incude in the version header.  Defaults to
the most recent version tag.  Must be in the format of C<vN.N.N> where
C<N> is a positive integer.

